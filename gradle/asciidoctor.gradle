apply plugin: 'org.asciidoctor.gradle.asciidoctor'  // so we can render AsciiDoc documents.
apply plugin: 'com.github.jruby-gradle.base'        // so we can use Ruby gems when rendering documents.

dependencies {
    // gems dependency configuration is added by
    // jruby-gradle-plugin. Here we define
    // the gems we need in our build.
    gems 'rubygems:asciidoctor-diagram:1.2.1'
}

asciidoctorj {
    // We can change the AsciidoctorJ 
    // dependency version.
    version = '1.5.2'
}

//
// NOTE: To actually run the asciidoctor task and generate SVG PlangUML images
// You'll need to install the GraphViz tool
// For Mac OS X use `brew install graphviz`
// For Ubuntu use: `apt-get install graphviz`
//

asciidoctor {
    sourceDir = file('doc')
    outputDir = file("$buildDir/adoc")
    separateOutputDirs = false

    // jrubyPrepareGems task is added by the JRuby 
    // plugin and will download Ruby gems we have
    // defined in the gems dependency configuration.
    dependsOn jrubyPrepare

    // Asciidoctor task needs the
    // asciidoctor-diagram gem, we installed
    // with the jrubyPrepareGems task and
    // gems dependency configuration.
    requires  'asciidoctor-diagram'

    // Set path to find gems to directory
    // used by jrubyPrepareGems task.
    gemPath = file('build/gems')
}
